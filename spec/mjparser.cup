
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:

	boolean errorDetected;

	Logger log = Logger.getLogger(getClass());

    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke

    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
    	done_parsing();
    	report_error(message, info);
    }

    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
		
:}

init with {:
	errorDetected = false;
:}

scan with {:

	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}



terminal Boolean BOOL;
terminal Integer NUMBER;
terminal String IDENT;
terminal Character CHAR;

nonterminal ActPar, ActPars, ActParsParens, Addop;
nonterminal Condition, CondFact, ConditionIf, CondTerm;
nonterminal ConstAssign, ConstDecl, ConstList;
nonterminal DesignatorStatement, ElseStart, FormPar;
nonterminal FormPars, FormParsParens, FunctionCall, FunctionName, IfStart, Label;
nonterminal MethodDecl, MethodDeclList, Mulop;
nonterminal ProgHeadList, ProgHead, Relop, Statement, StatementList;
nonterminal VarDecl, VarDeclList, VarList, WhileCondition, WhileStart;

terminal AND, BEQ, BREAK;
terminal CLASS, COMMA, CONST, CONT, DECR;
terminal DOT, ELSE, EQ, EQUAL, EXTENDS;
terminal IF, INCR, LAMBDA, LANGLE, LBRACE;
terminal LBRACKET, LEQ, LPAREN, MAP, MINUS;
terminal NEQ, NEW, OR, PERCENT;
terminal PLUS, PRINT, PROGRAM, RANGLE, RBRACE;
terminal RBRACKET, READ, RETURN, RPAREN, SEMI;
terminal SLASH, STAR, VOID, WHILE;

nonterminal rs.etf.pp1.symboltable.concepts.Obj ConstOne, Designator, DesignatorName, MapWrapper, MethodTypeAndName;
nonterminal rs.etf.pp1.symboltable.concepts.Obj VarDeclSingle, ProgName, Program;

nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr, Factor, Term, Type;

precedence left ELSE;


Program ::= (Program) PROGRAM ProgName:p ProgHeadList LBRACE MethodDeclList RBRACE;

ProgName ::= (ProgName) IDENT:progName;

ProgHeadList ::= ProgHeadList ProgHead
			|
			;

ProgHead ::= VarDecl
			|
			ConstDecl;

ConstDecl ::= (ConstDecl) CONST Type ConstList SEMI;

ConstOne ::= (ConstOneNumber) NUMBER
			|
			(ConstOneBool) BOOL
			|
			(ConstOneChar) CHAR;

ConstList ::= ConstList COMMA ConstAssign
			|
			ConstAssign;

ConstAssign ::= (ConstAssign) IDENT:name EQUAL ConstOne;

VarDeclList ::= VarDeclList VarDecl
			|
			;

VarDecl ::= Type VarList SEMI;

VarList ::= VarList COMMA VarDeclSingle
		|
		VarDeclSingle;

VarDeclSingle ::= (VarDeclScalar) IDENT:name
				|
				(VarDeclArray) IDENT:name LBRACKET RBRACKET
				|
				(VarDeclMatrix) IDENT:name LBRACKET RBRACKET LBRACKET RBRACKET;

MethodDeclList ::= MethodDeclList MethodDecl
				|
				;

MethodDecl ::= (MethodDecl) MethodTypeAndName FormParsParens VarDeclList LBRACE StatementList RBRACE;

MethodTypeAndName ::= (MethodTypeAndNameNonvoid) Type IDENT:name
			|
			(MethodTypeAndNameVoid) VOID IDENT:name;

FormPars ::= FormPars COMMA FormPar
			|
			FormPar;

FormParsParens ::= LPAREN FormPars RPAREN
				|
				LPAREN RPAREN;

FormPar ::= (FormParScalar) Type IDENT:name
		|
		(FormParArray) Type IDENT:name LBRACKET RBRACKET
		|
		(FormParMatrix) Type IDENT:name LBRACKET RBRACKET LBRACKET RBRACKET;

Type ::= (Type) IDENT:typeName;

Statement ::= DesignatorStatement SEMI
			|
			(StatementIf) IfStart Statement
			|
			(StatementElse) IfStart Statement ElseStart Statement
			|
			(StatementWhile) WhileCondition Statement
			|
			(StatementBreak) BREAK SEMI
			|
			(StatementContinue) CONT SEMI
			|
			(StatementReturnExpr) RETURN Expr SEMI
			|
			(StatementReturnVoid) RETURN SEMI
			|
			(StatementRead) READ LPAREN Designator RPAREN SEMI
			|
			(StatementPrintExpr) PRINT LPAREN Expr RPAREN SEMI
			|
			(StatementPrintNum) PRINT LPAREN Expr COMMA NUMBER RPAREN SEMI
			|
			(StatementMap) MapWrapper LAMBDA Expr RPAREN SEMI
			|
			LBRACE StatementList RBRACE;

IfStart ::= (IfStart) IF ConditionIf;

ElseStart ::= (ElseStart) ELSE;

MapWrapper ::= (MapWrapper) Designator EQUAL Designator DOT MAP LPAREN IDENT:iter;

WhileCondition ::= (WhileCondition) WhileStart ConditionIf;

WhileStart ::= (WhileStart) WHILE;

StatementList ::= StatementList Statement
				|
				;

DesignatorStatement ::= (DesignatorStatementAssign) Designator EQUAL Expr
					|
					(DesignatorStatementFuncCall) FunctionCall
					|
					(DesignatorStatementIncr) Designator INCR
					|
					(DesignatorStatementDecr) Designator DECR;

ActPars ::= ActPars COMMA ActPar
		|
		ActPar;

ActPar ::= (ActPar) Expr;

FunctionCall ::= (FunctionCall) FunctionName ActParsParens;

FunctionName ::= (FunctionName) Designator;

ActParsParens ::= LPAREN ActPars RPAREN
				|
				LPAREN RPAREN;

ConditionIf ::= LPAREN Condition RPAREN
			|
			LPAREN error RPAREN:l {: parser.report_error("Izvr≈°en oporavak do ')' za IF/WHILE u liniji " + lleft, null); :};

Condition ::= (ConditionOr) Condition OR CondTerm
			|
			CondTerm;

CondTerm ::= (CondTermAnd) CondTerm AND CondFact
		|
		CondFact;

CondFact ::= (CondFactRelop) Expr Relop Expr
		|
		(CondFactExpr) Expr;

Expr ::= (ExprAddop) Expr Addop Term
		|
		(ExprNegative) MINUS Term
		|
		(ExprTerm) Term;

Term ::= (TermMulop) Term Mulop Factor
		|
		(TermFactor) Factor;

Factor ::= (FactorDesignator) Designator
		|
		(FactorFuncCall) FunctionCall
		|
		(FactorConst) ConstOne
		|
		(FactorNewArray) NEW Type LBRACKET Expr RBRACKET
		|
		(FactorNewMatrix) NEW Type LBRACKET Expr RBRACKET LBRACKET Expr RBRACKET
		|
		(FactorExpr) LPAREN Expr RPAREN;

Designator ::= (DesignatorScalar) DesignatorName
			|
			(DesignatorArray) DesignatorName LBRACKET Expr RBRACKET
			|
			(DesignatorMatrix) DesignatorName LBRACKET Expr RBRACKET LBRACKET Expr RBRACKET;

DesignatorName ::= (DesignatorName) IDENT:name;

Label ::= IDENT;

Relop ::= (RelopEQ) EQ
		|
		(RelopNEQ) NEQ
		|
		(RelopGT) RANGLE
		|
		(RelopGE) BEQ
		|
		(RelopLT) LANGLE
		|
		(RelopLE) LEQ;

Addop ::= (AddopPlus) PLUS
		|
		(AddopMinus) MINUS;

Mulop ::= (MulopMul) STAR
		|
		(MulopDiv) SLASH
		|
		(MulopMod) PERCENT;

